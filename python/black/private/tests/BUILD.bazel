load("@rules_req_compile//:defs.bzl", "py_reqs_compiler", "py_reqs_solution_test")
load("//python/black:defs.bzl", "py_black_test")

PLATFORMS = [
    "linux",
    "macos",
    "windows",
]

filegroup(
    name = "test_requirements_in",
    srcs = ["test_requirements.in"],
    data = ["//python/black/private/3rdparty:requirements.in"],
)

[
    py_reqs_compiler(
        name = "test_requirements.{}.update".format(platform),
        requirements_in = "test_requirements_in",
        requirements_txt = "test_requirements_{}.txt".format(platform),
        target_compatible_with = ["@platforms//os:{}".format(platform)],
    )
    for platform in PLATFORMS
]

[
    py_reqs_solution_test(
        name = "test_requirements_{}_test".format(platform),
        compiler = ":test_requirements.{}.update".format(platform),
    )
    for platform in PLATFORMS
]

alias(
    name = "test_requirements.update",
    actual = select({
        "@platforms//os:{}".format(platform): ":test_requirements.{}.update".format(platform)
        for platform in PLATFORMS
    }),
)

py_black_test(
    name = "black_fixer_black_test",
    target = "//python/black/private:black_fixer",
)

py_black_test(
    name = "black_runner_black_test",
    target = "//python/black/private:black_runner",
)
